import java.nio.file.Paths

def config = project.extensions.findByName("react") ?: [:]
def bundleAssetName = config.bundleAssetName ? config.bundleAssetName.get() : "index.android.bundle"

def elvisFile(thing) {
    return thing ? file(thing) : null
}

void runBefore(String dependentTaskName, Task task) {
    Task dependentTask = tasks.findByPath(dependentTaskName)
    if (dependentTask != null) {
        dependentTask.dependsOn task
    }
}

static def findNodeModulePath(baseDir, packageName) {
    def basePath = baseDir.toPath().normalize()
    while (basePath) {
        def candidatePath = Paths.get(basePath.toString(), "node_modules", packageName)
        if (candidatePath.toFile().exists()) {
            return candidatePath.toString()
        }
        basePath = basePath.getParent()
    }
    return null
}

android.buildTypes.each { buildType ->
    buildType.resValue 'string', "CODE_PUSH_APK_BUILD_TIME", String.format("\"%d\"", System.currentTimeMillis())
}

gradle.projectsEvaluated {
    def debuggableVariants = config.debuggableVariants ? config.debuggableVariants.get() : ['debug']

    android.applicationVariants.all { variant ->
        if (debuggableVariants.contains(variant.name)) {
            return
        }

        def nodeModulesPath
        if (project.hasProperty('nodeModulesPath')) {
            nodeModulesPath = "${project.nodeModulesPath}/react-native-codepush-sdk"
        } else {
            nodeModulesPath = findNodeModulePath(projectDir, "react-native-codepush-sdk")
        }

        if (!nodeModulesPath) {
            throw new GradleException("Cannot find react-native-codepush-sdk in node_modules")
        }

        def targetName = variant.name.capitalize()
        def targetPath = variant.dirName

        def jsBundleDir
        def resourcesDir
        def jsBundleFile

        def nodeExecutableAndArgs = config.nodeExecutableAndArgs ? config.nodeExecutableAndArgs.get() : ["node"]
        def extraPackagerArgs = config.extraPackagerArgs ? config.extraPackagerArgs.get() : []

        def reactBundleTask = tasks.findByName("createBundle${targetName}JsAndAssets")
        if (reactBundleTask) {
            jsBundleDir = reactBundleTask.property('jsBundleDir').asFile.get()
            resourcesDir = reactBundleTask.property('resourcesDir').asFile.get()

            project.delete(files("${jsBundleDir}"))
            jsBundleDir.mkdirs()
            resourcesDir.mkdirs()

            jsBundleFile = file("$jsBundleDir/$bundleAssetName")

            def generateBundledResourcesHash = tasks.create(
                name: "generateBundledResourcesHash${targetName}",
                type: Exec) {
                commandLine (*nodeExecutableAndArgs, "${nodeModulesPath}/scripts/generateBundledResourcesHash.js", resourcesDir, jsBundleFile, jsBundleDir)
                enabled !debuggableVariants.contains(variant.name) ?: targetName.toLowerCase().contains("release")
            }

            runBefore("merge${targetName}Resources", generateBundledResourcesHash)
            runBefore("merge${targetName}Assets", generateBundledResourcesHash)
        } else {
            def jsBundleDirConfigName = "jsBundleDir${targetName}"
            jsBundleDir = elvisFile(config."$jsBundleDirConfigName") ? elvisFile(config."$jsBundleDirConfigName").get() :
                file("$buildDir/intermediates/assets/${targetPath}")

            def resourcesDirConfigName = "resourcesDir${targetName}"
            resourcesDir = elvisFile(config."${resourcesDirConfigName}") ? elvisFile(config."${resourcesDirConfigName}").get() :
                file("$buildDir/intermediates/res/merged/${targetPath}")

            if (!resourcesDir.exists() && file("$buildDir/intermediates/res/${targetPath}").exists()) {
                resourcesDir = file("$buildDir/intermediates/res/${targetPath}")
            }

            jsBundleFile = file("$jsBundleDir/$bundleAssetName")

            def resourcesMapTempFileName = "CodePushResourcesMap-" + java.util.UUID.randomUUID().toString().substring(0,8) + ".json"

            def generateBundledResourcesHash = tasks.create(
                name: "generateBundledResourcesHash${targetName}",
                type: Exec) {
                commandLine (*nodeExecutableAndArgs, "${nodeModulesPath}/scripts/generateBundledResourcesHash.js", resourcesDir, jsBundleFile, jsBundleDir, resourcesMapTempFileName)
            }

            def recordFilesBeforeBundleCommand = tasks.create(
                name: "recordFilesBeforeBundleCommand${targetName}",
                type: Exec) {
                commandLine (*nodeExecutableAndArgs, "${nodeModulesPath}/scripts/recordFilesBeforeBundleCommand.js", resourcesDir, resourcesMapTempFileName)
            }

            recordFilesBeforeBundleCommand.dependsOn("merge${targetName}Resources")
            recordFilesBeforeBundleCommand.dependsOn("merge${targetName}Assets")
            runBefore("bundle${targetName}JsAndAssets", recordFilesBeforeBundleCommand)

            generateBundledResourcesHash.dependsOn("recordFilesBeforeBundleCommand${targetName}")
        }

        generateBundledResourcesHash.dependsOn("createBundle${targetName}JsAndAssets")

        runBefore("processArmeabi-v7a${targetName}Resources", generateBundledResourcesHash)
        runBefore("processX86${targetName}Resources", generateBundledResourcesHash)
        runBefore("processUniversal${targetName}Resources", generateBundledResourcesHash)
        runBefore("process${targetName}Resources", generateBundledResourcesHash)
    }
}